-- vim: filetype=dhall
--
-- ```
-- TOOLSET [--verbosity=VERBOSITY] [--colour=COLOUR_OUTPUT] [--[no-]aliases]
--     [--change-directory=DIRECTORY] -- completion --index=INDEX --shell=SHELL
--     -- [WORD ...]
-- ```

let GlobalOptions =
        ./GlobalOptions/package.dhall sha256:9fd9054401b8aa5fdd448fb454ddc08e1d2c8696dd4560f7e7933af395fe0501
      ? ./GlobalOptions/package.dhall

let ExecCommand =
      { Type =
            ../../CommandWrapper/ExecCommand/Type sha256:6ece797a2c269b469da41f12ec2d7206846cac65183ae8213cce1b6d59f2b02b
          ? ../../CommandWrapper/ExecCommand/Type
      , default =
            ../../CommandWrapper/ExecCommand/default sha256:c3a088ca2b090c91d5d630c4e01f4b4fbd0136f5bb1251f6828698e5180685b2
          ? ../../CommandWrapper/ExecCommand/default
      }

let Shell =
        ../../CommandWrapper/Shell/Type sha256:f61ef033bfb850ef4bf0c3d0d18c69d1b15b38cc9e3df4a1abea334b89ed5555
      ? ../../CommandWrapper/Shell/Type

let Shell/toText =
        ../../CommandWrapper/Shell/toText sha256:ac2b3762009ee945aac998034d4b44cb47fff0593df4547c9423dd62e54e92d4
      ? ../../CommandWrapper/Shell/toText

let Verbosity =
        ../../CommandWrapper/Verbosity/Type sha256:f009a39a49b1ee65651e9510778e7d72ff96820f4702a955e8f47682d72995c6
      ? ../../CommandWrapper/Verbosity/Type

in    λ(toolset : Text)
    → λ(options : GlobalOptions.Type)
    → λ(prefixArguments : List Text)
    → λ(shell : Shell)
    → λ(index : Natural)
    → λ(words : List Text)
    → let adjustedIndex = index + List/length Text prefixArguments

      let adjustedOptions =
              options
            ⫽ { verbosity = Some Verbosity.Silent, allowAliases = Some False }

      in  ExecCommand::{
          , command = toolset
          , arguments =
                GlobalOptions.toArguments adjustedOptions
              # [ "--"
                , "completion"
                , "--index=${Natural/show adjustedIndex}"
                , "--shell=${Shell/toText shell}"
                , "--"
                ]
              # prefixArguments
              # words
          , environment = GlobalOptions.toEnvironment adjustedOptions
          }
