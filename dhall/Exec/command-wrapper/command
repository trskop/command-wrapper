-- vim: filetype=dhall
--
-- ```
-- TOOLSET [--verbosity=VERBOSITY] [--colour=COLOUR_OUTPUT] [--[no-]aliases]
--     [--change-directory=DIRECTORY] -- [SUBCOMMAND [ARGUMENT ...]]
-- ```

let GlobalOptions =
        ./GlobalOptions/package.dhall sha256:ed4f1162592b84a06fe2803b4c24acc759635c55c8b9b24dc93c4b28f0867aeb
      ? ./GlobalOptions/package.dhall

let ExecCommand =
      { Type =
            ../../CommandWrapper/ExecCommand/Type sha256:6ece797a2c269b469da41f12ec2d7206846cac65183ae8213cce1b6d59f2b02b
          ? ../../CommandWrapper/ExecCommand/Type
      , default =
            ../../CommandWrapper/ExecCommand/default sha256:c3a088ca2b090c91d5d630c4e01f4b4fbd0136f5bb1251f6828698e5180685b2
          ? ../../CommandWrapper/ExecCommand/default
      }

let ColourOutput =
        ../../CommandWrapper/ColourOutput/Type sha256:0ef91a3f044406ee80fc20f26127b448a0e98f46c46ec024457023d2aded8543
      ? ../../CommandWrapper/ColourOutput/Type

let Verbosity =
        ../../CommandWrapper/Verbosity/Type sha256:f009a39a49b1ee65651e9510778e7d72ff96820f4702a955e8f47682d72995c6
      ? ../../CommandWrapper/Verbosity/Type

in    λ(toolset : Text)
    → λ(options : GlobalOptions.Type)
    → λ(verbosity : Verbosity)
    → λ(colourOutput : ColourOutput)
    → λ(arguments : List Text)
    → ExecCommand::{
      , command = toolset
      , arguments =
            GlobalOptions.toArguments
              (   options
                ⫽ { verbosity =
                      Optional/fold
                        Verbosity
                        options.verbosity
                        (Optional Verbosity)
                        (λ(_ : Verbosity) → Some _)
                        (Some verbosity)
                  , colour =
                      Optional/fold
                        ColourOutput
                        options.colour
                        (Optional ColourOutput)
                        (λ(_ : ColourOutput) → Some _)
                        (Some colourOutput)
                  }
              )
          # [ "--" ]
          # arguments
      }
