let CommandWrapper = ../../CommandWrapper/Types.dhall

let Verbosity/fold = (../../CommandWrapper/package.dhall).verbosity.fold

in    λ(connect : CommandWrapper.ConnectToDatabase)
    → λ(environment : List CommandWrapper.EnvironmentVariable)
    → λ(verbosity : CommandWrapper.Verbosity)
    → λ(_ : CommandWrapper.ColourOutput)
    → λ(arguments : List Text)
    → { command = "pg_dump"
      , arguments =
          Verbosity/fold (List Text)
            { Silent = [] : List Text
            , Normal = [] : List Text
            , Verbose = ["--verbose"]
            , Annoying = ["--verbose"]
            }
            verbosity
          # [ "--host=${connect.hostname}"
            , "--username=${connect.username}"
            , "--dbname=${connect.database}"
            ]
          # arguments
      , environment = environment
      , searchPath = True
      , workingDirectory = None Text
      } : CommandWrapper.ExecCommand

-- vim: filetype=dhall
