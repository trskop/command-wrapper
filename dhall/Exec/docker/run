-- vim: filetype=dhall
--
-- Transform a command into:
--
-- ```
-- docker GLOBAL_OPTIONS run RUN_OPTIONS CONTAINER COMMAND
-- ```

let CommandWrapper = ../../CommandWrapper/Types.dhall

in    λ(container : Text)
    → λ(globalOptions : ./DockerGlobalOptions)
    → λ(runOptions : ./DockerRunOptions)
    → λ ( mkCommand
        :   ∀(environment : List CommandWrapper.EnvironmentVariable)
          → ∀(verbosity : CommandWrapper.Verbosity)
          → ∀(colourOutput : CommandWrapper.ColourOutput)
          → ∀(arguments : List Text)
          → CommandWrapper.ExecCommand
        )
    → λ(environment : List CommandWrapper.EnvironmentVariable)
    → λ(verbosity : CommandWrapper.Verbosity)
    → λ(colourOutput : CommandWrapper.ColourOutput)
    → λ(arguments : List Text)
    → let command = mkCommand environment verbosity colourOutput arguments
      
      in    { command = "docker"
            , arguments =
                  ./globalOptions globalOptions
                # [ "run" ]
                # ./runOptions runOptions
                # [ container ]
                # ./envOptions command.environment
                # [ command.command ]
                # command.arguments
            , environment = [] : List CommandWrapper.EnvironmentVariable
            , searchPath = True
            , workingDirectory = None Text
            }
          : CommandWrapper.ExecCommand
