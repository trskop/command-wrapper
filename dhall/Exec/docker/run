-- vim: filetype=dhall
--
-- Transform a command into:
--
-- ```
-- docker GLOBAL_OPTIONS run RUN_OPTIONS CONTAINER COMMAND
-- ```

let ExecCommand =
      { Type =
            ../../CommandWrapper/ExecCommand/Type sha256:6ece797a2c269b469da41f12ec2d7206846cac65183ae8213cce1b6d59f2b02b
          ? ../../CommandWrapper/ExecCommand/Type
      , default =
            ../../CommandWrapper/ExecCommand/default sha256:c3a088ca2b090c91d5d630c4e01f4b4fbd0136f5bb1251f6828698e5180685b2
          ? ../../CommandWrapper/ExecCommand/default
      }

let ColourOutput =
        ../../CommandWrapper/ColourOutput/Type sha256:0ef91a3f044406ee80fc20f26127b448a0e98f46c46ec024457023d2aded8543
      ? ../../CommandWrapper/ColourOutput/Type

let Verbosity =
        ../../CommandWrapper/Verbosity/Type sha256:f009a39a49b1ee65651e9510778e7d72ff96820f4702a955e8f47682d72995c6
      ? ../../CommandWrapper/Verbosity/Type

let Environment =
        ./Environment/Type sha256:bfc2cb080bb0cac5a42a81beb707437389fa9c6f8e54ae8dd7ce09c6566140ee
      ? ./Environment/Type

let GlobalOptions =
        ./GlobalOptions/Type sha256:88e6fa8fe07e5d00621b8c6de87f845d885fd4c2fc9be493cdd8a66ddfa7f524
      ? ./GlobalOptions/Type

let RunOptions =
        ./RunOptions/Type sha256:df4fdf5b50b4b3bdcd0e021533de4d6333432518d41bbea6146d9b73847a7892
      ? ./RunOptions/Type

let GlobalOptions/toArguments =
        ./GlobalOptions/toArguments sha256:150c73831857ece522d3f066554ad2883e36ceb2f98bf85947c8eacf0e09cb0b
      ? ./GlobalOptions/toArguments

let RunOptions/toArguments =
        ./RunOptions/toArguments sha256:5fdea1f964ab7b935cf9a80e0f375fe0d0e75c3459a0bb3200d4492ee12d2895
      ? ./RunOptions/toArguments

let Environment/toArguments =
        ./Environment/toArguments sha256:d0426badaef8cd6535378bbd9b5caf073e2b7d794eae541a454ebcba4cb52531
      ? ./Environment/toArguments

in    λ(container : Text)
    → λ(globalOptions : GlobalOptions)
    → λ(runOptions : RunOptions)
    → λ ( mkCommand
        :   ∀(environment : Environment)
          → ∀(verbosity : Verbosity)
          → ∀(colourOutput : ColourOutput)
          → ∀(arguments : List Text)
          → ExecCommand.Type
        )
    → λ(environment : Environment)
    → λ(verbosity : Verbosity)
    → λ(colourOutput : ColourOutput)
    → λ(arguments : List Text)
    → let command = mkCommand environment verbosity colourOutput arguments
      
      in  ExecCommand::{
          , command = "docker"
          , arguments =
                GlobalOptions/toArguments globalOptions
              # [ "run" ]
              # RunOptions/toArguments runOptions
              # [ container ]
              # Environment/toArguments command.environment
              # [ command.command ]
              # command.arguments
          }
