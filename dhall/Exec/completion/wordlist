-- vim: filetype=dhall
--
-- ```
-- bash -c "compgen -W \"${wordlist}\" -- '${words[${index}]}'"
-- ```

let ExecCommand =
      { Type =
            ../../CommandWrapper/ExecCommand/Type sha256:6ece797a2c269b469da41f12ec2d7206846cac65183ae8213cce1b6d59f2b02b
          ? ../../CommandWrapper/ExecCommand/Type
      , default =
            ../../CommandWrapper/ExecCommand/default sha256:c3a088ca2b090c91d5d630c4e01f4b4fbd0136f5bb1251f6828698e5180685b2
          ? ../../CommandWrapper/ExecCommand/default
      }

let Shell =
        ../../CommandWrapper/Shell/Type sha256:f61ef033bfb850ef4bf0c3d0d18c69d1b15b38cc9e3df4a1abea334b89ed5555
      ? ../../CommandWrapper/Shell/Type

let List/index =
        ../../CommandWrapper/List/index sha256:275705f96ae6d2a2e2194489a9a3c9798fa9e7d6f7ff6847077d46efa3519e15
      ? ../../CommandWrapper/List/index

let Optional/from =
        ../../CommandWrapper/Optional/from sha256:8f802473931b605422b545d7b81de20dbecb38f2ae63950c13f5381865a7f012
      ? ../../CommandWrapper/Optional/from

in    λ(wordlist : List Text)
    → λ(_ : Shell)
    → λ(index : Natural)
    → λ(words : List Text)
    → ExecCommand::{
      , command = "bash"
      , arguments =
          let completions =
                List/fold
                  Text
                  wordlist
                  Text
                  (λ(word : Text) → λ(acc : Text) → "${acc} ${word}")
                  ""

          let pattern = Optional/from Text "" (List/index index Text words)

          in  [ "-c", "compgen -W \"${completions}\" -- '${pattern}'" ]
      }
