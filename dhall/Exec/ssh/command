let CommandWrapper = ../../CommandWrapper/Types.dhall

in    λ(options : ./Options)
    → λ ( connectTo
        : { user : Optional Text, host : Text, port : Optional Natural }
        )
    → λ(environment : List CommandWrapper.EnvironmentVariable)
    → λ(extraOptions : List Text)
    → λ(verbosity : CommandWrapper.Verbosity)
    → λ(colourOutput : CommandWrapper.ColourOutput)
    → λ(arguments : List Text)
    →   { command =
            "ssh"
        , arguments =
              (./options options : List Text)
            # Optional/fold
              Natural
              connectTo.port
              (List Text)
              (λ(_ : Natural) → [ "-p", Natural/show _ ])
              ([] : List Text)
            # extraOptions
            # [ Optional/fold
                Text
                connectTo.user
                Text
                (λ(_ : Text) → "${_}@${connectTo.host}")
                connectTo.host
              ]
            # [ "--" ]
            # arguments
        , environment =
            environment
        , searchPath =
            True
        , workingDirectory =
            None Text
        }
      : CommandWrapper.ExecCommand

-- vim: filetype=dhall
