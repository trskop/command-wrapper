let Forwarding = ./Forwarding

let ForwardingOptions = ./ForwardingOptions

let DynamicForwardingOptions = ./DynamicForwardingOptions

let hostAndPort =
        λ(_ : { host : Optional Text, port : Natural })
      →     Optional/fold Text _.host Text (λ(_ : Text) → _ ++ ":") ""
        ++  Natural/show _.port

let forwardingOptions =
        λ(_ : ForwardingOptions)
      →     ( merge
              { HostAndPort =
                  hostAndPort
              , UnixSocket =
                  λ(_ : { path : Text }) → _.path
              }
              _.listenOn
            )
        ++  ":${_.connectTo.host}.${Natural/show _.connectTo.port}"

let dynamicForwarding =
        λ(_ : DynamicForwardingOptions)
      →     Optional/fold Text _.bindTo Text (λ(_ : Text) → _ ++ ":") ""
        ++  Natural/show _.listenOn

let forwarding =
        λ(_ : Forwarding)
      → merge
        { Local =
            λ(_ : ForwardingOptions) → [ "-L", forwardingOptions _ ]
        , Remote =
            λ(_ : ForwardingOptions) → [ "-R", forwardingOptions _ ]
        , Dynamic =
            λ(_ : DynamicForwardingOptions) → [ dynamicForwarding _ ]
        }
        _

let List/concatMap =
      https://prelude.dhall-lang.org/List/concatMap
      sha256:3b2167061d11fda1e4f6de0522cbe83e0d5ac4ef5ddf6bb0b2064470c5d3fb64

let optional =
        λ(a : Type)
      → λ(o : Optional a)
      → λ(f : a → List Text)
      → Optional/fold a o (List Text) f ([] : List Text)

in    λ(options : ./Options)
    →   optional Text options.identityFile (λ(_ : Text) → [ "-i", _ ])
      # List/concatMap Forwarding Text forwarding options.forwardings
      # optional Text options.configFile (λ(_ : Text) → [ "-F", _ ])
      # optional
        Bool
        options.allocatePseudoTerminal
        (λ(_ : Bool) → if _ then [ "-t" ] else [ "-T" ])
      # (if options.doNotExecuteRemoteCommand then [ "-N" ] else [] : List Text)
      # (if options.preventReadingOfStdin then [ "-n" ] else [] : List Text)

-- vim: filetype=dhall
