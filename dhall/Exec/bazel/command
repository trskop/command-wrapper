-- vim: filetype=dhall

let ExecCommand =
      { Type =
            ../../CommandWrapper/ExecCommand/Type sha256:6ece797a2c269b469da41f12ec2d7206846cac65183ae8213cce1b6d59f2b02b
          ? ../../CommandWrapper/ExecCommand/Type
      , default =
            ../../CommandWrapper/ExecCommand/default sha256:c3a088ca2b090c91d5d630c4e01f4b4fbd0136f5bb1251f6828698e5180685b2
          ? ../../CommandWrapper/ExecCommand/default
      }

let ColourOutput =
        ../../CommandWrapper/ColourOutput/Type sha256:0ef91a3f044406ee80fc20f26127b448a0e98f46c46ec024457023d2aded8543
      ? ../../CommandWrapper/ColourOutput/Type

let Verbosity =
        ../../CommandWrapper/Verbosity/Type sha256:f009a39a49b1ee65651e9510778e7d72ff96820f4702a955e8f47682d72995c6
      ? ../../CommandWrapper/Verbosity/Type

let ColourOutput/toText =
        ( ../../CommandWrapper/ColourOutput/package.dhall sha256:ab063b4e29e5237c1bbb2c4ed07ed3cc80096c9cd2f9978d727d81ac1f1c8c68
        ).toText
      ? (   ../../CommandWrapper/ColourOutput/package.dhall sha256:8fd5555f273cd4424f8fe2003154c4e36ea657bb5a2642d3677c4756b14c32e8
          ? ../../CommandWrapper/ColourOutput/package.dhall
        ).toText

let Verbosity/fold =
        ../../CommandWrapper/Verbosity/fold sha256:4dac2c264a2531d569ad0e5f712a1cd2d17b51ecdc502cc72f19937bf4733b1e
      ? ../../CommandWrapper/Verbosity/fold

let List/head-and-tail =
        ../../CommandWrapper/List/head-and-tail sha256:9e5de1b61d63f7caf1b197df7eb0ca19f682e6c05b4e1f557cd5a88355b31b08
      ? ../../CommandWrapper/List/head-and-tail

let verbosityOptions =
      Verbosity/fold
        (List Text)
        { Silent = [ "--logging=0" ]
        , Normal = [] : List Text
        , Verbose = [ "--logging=5", "--explain" ]
        , Annoying =
            [ "--logging=6"
            , "--explain"
            , "--verbose_explanations"
            , "--verbose_failures"
            ]
        }

let colorOption =
        λ(colourOutput : ColourOutput)
      → "--color=${ColourOutput/toText colourOutput}"

in    λ(workingDirectory : Optional Text)
    → λ(args : List Text)
    → λ(verbosity : Verbosity)
    → λ(colourOutput : ColourOutput)
    → λ(extraArgs : List Text)
    → ExecCommand::{
      , command = "bazel"
      , arguments =
          let arguments = List/head-and-tail Text (args # extraArgs)

          in    verbosityOptions verbosity
              # Optional/fold
                  Text
                  arguments.head
                  (List Text)
                  (λ(cmd : Text) → [ cmd, colorOption colourOutput ])
                  ([] : List Text)
              # arguments.tail
      , workingDirectory = workingDirectory
      }
