let CommandWrapper = ~/.config/command-wrapper/Types.dhall

let commandWrapper = ~/.config/command-wrapper/library.dhall

let noExtraEnvironment = commandWrapper.command.emptyEnvironment

let Shell/toText =
        λ(shell : CommandWrapper.Shell)
      → merge { Bash = "bash", Fish = "fish", Zsh = "zsh" } shell

let mkArguments =
        λ(shell : CommandWrapper.Shell)
      → λ(index : Natural)
      → λ(words : List Text)
      →   [ "--index=${Natural/show index}"
          , "--shell=${merge
                       { Bash = "bash", Fish = "fish", Zsh = "zsh" }
                       shell}"
          , "--"
          ]
        # words

let script =
      { url =
          "https://raw.githubusercontent.com/trskop/command-wrapper/master/dhall/Exec/completion/yarn.bash.dhall"
      , hash =
          "sha256:1d434192b61a243cc7d1a311102caedb05b8d28d74466b8ba3ecdaebd973fb1e"
      }

in    λ(toolsetCommand : Text)
    → λ(workingDirectory : Optional Text)
    → λ(prefixArguments : List Text)
    → λ(shell : CommandWrapper.Shell)
    → λ(index : Natural)
    → λ(words : List Text)
    → { command =
          toolsetCommand
      , arguments =
          let adjusted-index = index + List/length Text prefixArguments
          
          let arguments =
                mkArguments
                shell
                adjusted-index
                ([ "yarn" ] # prefixArguments # words)
          
          in    [ "completion"
                , "--wrapper"
                , "--expression=${script.url} ${script.hash} : Text"
                , "--exec"
                , "--"
                ]
              # arguments
      , environment =
          noExtraEnvironment
      , searchPath =
          True
      , workingDirectory =
          workingDirectory
      }

-- vim: filetype=dhall
