-- vim: filetype=dhall

let ExecCommand =
        ../../CommandWrapper/ExecCommand/Type sha256:6ece797a2c269b469da41f12ec2d7206846cac65183ae8213cce1b6d59f2b02b
      ? ../../CommandWrapper/ExecCommand/Type

let Verbosity =
        ../../CommandWrapper/Verbosity/Type sha256:f009a39a49b1ee65651e9510778e7d72ff96820f4702a955e8f47682d72995c6
      ? ../../CommandWrapper/Verbosity/Type

let ColourOutput =
        ../../CommandWrapper/ColourOutput/Type sha256:0ef91a3f044406ee80fc20f26127b448a0e98f46c46ec024457023d2aded8543
      ? ../../CommandWrapper/ColourOutput/Type

let EnvironmentVariable =
        ../../CommandWrapper/EnvironmentVariable/Type sha256:b8c3c0c4ceb36ba4e6674df5de20ad1d97e120b93b9ce9914a41d0036770dcc4
      ? ../../CommandWrapper/EnvironmentVariable/Type

let emptyArguments = [] : List Text

let emptyEnvironment = [] : List EnvironmentVariable

in    λ(changeDirectory : Optional Text)
    → λ(prefixArguments : List Text)
    → λ(_ : Verbosity)
    → λ(colourOutput : ColourOutput)
    → λ(arguments : List Text)
    →   { command = "yarn"
        , arguments =
              Optional/fold
                Text
                changeDirectory
                (List Text)
                (λ(_ : Text) → [ "--cwd=${_}" ])
                emptyArguments
            # prefixArguments
            # arguments
        , environment = emptyEnvironment
        , searchPath = True
        , workingDirectory = None Text
        }
      : ExecCommand
