-- vim: filetype=dhall

let CommandWrapper = ../../CommandWrapper/Types.dhall

let commandWrapper = ../../CommandWrapper/package.dhall

let emptyArguments = commandWrapper.command.emptyArguments

let downOptions = ./downOptions

let upOptions = ./upOptions

let Action = ./Action

let GlobalOptions = ./GlobalOptions

let globalOptions = ./globalOptions

in    λ(options : GlobalOptions)
    → λ(action : Optional Action)
    → λ(_ : CommandWrapper.Verbosity)
    → λ(colourOutput : CommandWrapper.ColourOutput)
    → λ(arguments : List Text)
    → { command = "docker-compose"
      , arguments =
            globalOptions options
          # Optional/fold
              Action
              action
              (List Text)
              (   λ(_ : Action)
                → merge { Down = downOptions, Up = upOptions colourOutput } _
              )
              emptyArguments
          # arguments
      , environment = commandWrapper.command.emptyEnvironment
      , searchPath = True
      , workingDirectory = None Text
      }
