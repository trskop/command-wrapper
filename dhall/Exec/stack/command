-- vim: filetype=dhall

let ExecCommand =
        ../../CommandWrapper/ExecCommand/Type sha256:6ece797a2c269b469da41f12ec2d7206846cac65183ae8213cce1b6d59f2b02b
      ? ../../CommandWrapper/ExecCommand/Type

let ColourOutput =
        ../../CommandWrapper/ColourOutput/Type sha256:0ef91a3f044406ee80fc20f26127b448a0e98f46c46ec024457023d2aded8543
      ? ../../CommandWrapper/ColourOutput/Type

let Verbosity =
        ../../CommandWrapper/Verbosity/Type sha256:f009a39a49b1ee65651e9510778e7d72ff96820f4702a955e8f47682d72995c6
      ? ../../CommandWrapper/Verbosity/Type

let EnvironmentVariable =
        ../../CommandWrapper/EnvironmentVariable/Type sha256:b8c3c0c4ceb36ba4e6674df5de20ad1d97e120b93b9ce9914a41d0036770dcc4
      ? ../../CommandWrapper/EnvironmentVariable/Type

let ColourOutput/toText =
        ( ../../CommandWrapper/ColourOutput/package.dhall sha256:ab063b4e29e5237c1bbb2c4ed07ed3cc80096c9cd2f9978d727d81ac1f1c8c68
        ).toText
      ? ( ../../CommandWrapper/ColourOutput/package.dhall).toText

let Verbosity/fold =
        ../../CommandWrapper/Verbosity/fold sha256:4dac2c264a2531d569ad0e5f712a1cd2d17b51ecdc502cc72f19937bf4733b1e
      ? ../../CommandWrapper/Verbosity/fold

let verbosityOption =
      Verbosity/fold
        (List Text)
        { Silent = [ "--silent" ]
        , Normal = [] : List Text
        , Verbose = [ "--verbosity=info" ]
        , Annoying = [ "--verbosity=debug" ]
        }

let mkStackYamlOption = 
        ./stackYamlOption sha256:7579242492aacd52e17f76c0a07f2440f1ca690550746bbaba88bb970e2ddd30
      ? ./stackYamlOption

let colorOption =
        λ(colourOutput : ColourOutput)
      → [ "--color=${ColourOutput/toText colourOutput}" ]

in    λ ( context
        : { workingDirectory : Optional Text, stackYaml : Optional Text }
        )
    → λ(args : List Text)
    → λ(verbosity : Verbosity)
    → λ(colourOutput : ColourOutput)
    → λ(arguments : List Text)
    →   { command = "stack"
        , arguments =
              verbosityOption verbosity
            # colorOption colourOutput
            # mkStackYamlOption context.stackYaml
            # args
            # arguments
        , environment = [] : List EnvironmentVariable
        , searchPath = True
        , workingDirectory = context.workingDirectory
        }
      : ExecCommand
