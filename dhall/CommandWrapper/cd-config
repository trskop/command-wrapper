let CommandWrapper = ./Types.dhall

let terminal-emulator/urxvt = ./terminal-emulator/urxvt

let emptyDirectories = [] : List Text

let systemShell = None Text

let menuTools =
      { fzf =
            λ(query : Optional Text)
          → { command = "fzf"
            , arguments =
                  ["--reverse"]
                # Optional/fold Text query (List Text)
                    (λ(query : Text) → ["--query=${query}"])
                    ([] : List Text)
            , environment = [] : List CommandWrapper.EnvironmentVariable
            } : CommandWrapper.CommandWithEnvironment

      , fzy =
            λ(query : Optional Text)
          → { command = "fzy"
            , arguments =
                Optional/fold Text query (List Text)
                  (λ(query : Text) → ["--query=${query}"])
                  ([] : List Text)
            , environment = [] : List CommandWrapper.EnvironmentVariable
            } : CommandWrapper.CommandWithEnvironment
      }

in  { defaults =
          { directories = emptyDirectories
          , menuTool = menuTools.fzf
          , shell = systemShell
          , terminalEmulator =
                λ(directory : Text)
              → λ(command : Optional CommandWrapper.Command)
              → terminal-emulator/urxvt (Some directory) command
              : CommandWrapper.CommandWithEnvironment
          }
        : CommandWrapper.CdConfig

    , emptyDirectories = emptyDirectories
    , systemShell = systemShell
    , menuTools = menuTools
    }

-- vim: filetype=dhall
