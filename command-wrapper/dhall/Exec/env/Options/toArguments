-- vim: filetype=dhall

let Options =
        ./Type sha256:e7f1c7efc903b4e3bd08a7a9cbff0bd56d7f5062e98e881dd93523d2ae874b27
      ? ./Type

let default = ./default

let EnvironmentVariable =
        ../../../CommandWrapper/EnvironmentVariable/package.dhall sha256:1250426124fe1a06fce13386a45e803655f7fe419fb372bd4a5157993350b199
      ? ../../../CommandWrapper/EnvironmentVariable/package.dhall

let concatMap =
      https://prelude.dhall-lang.org/v17.0.0/List/concatMap sha256:3b2167061d11fda1e4f6de0522cbe83e0d5ac4ef5ddf6bb0b2064470c5d3fb64

let toArguments =
      λ(options : Options) →
          (if options.ignoreEnvironment then [ "-i" ] else [] : List Text)
        # concatMap Text Text (λ(name : Text) → [ "-u", name ]) options.undefine
        # concatMap
            EnvironmentVariable.Type
            Text
            ( λ(var : EnvironmentVariable.Type) →
                [ EnvironmentVariable.toText var ]
            )
            options.define

let test0 =
        assert
      :   toArguments
            { Type = Options, default }::{
            , ignoreEnvironment = True
            , undefine = [ "FOO" ]
            , define = [ { name = "BAR", value = "baz" } ]
            }
        ≡ [ "-i", "-u", "FOO", "BAR=baz" ]

in  toArguments : Options → List Text
