-- vim: filetype=dhall

let Options =
        ./Type sha256:19bdbe3076e343351322b9fb0447e422939c239c1f11819e44bdfcd6684670a7
      ? ./Type

let Options/default =
        ./default sha256:fc01c8e6de2e973fafedb63b67b29ba41fe06c8078af2454a1c0e370884226e0
      ? ./default

let Action =
        ../Action/Type sha256:8df4bf8c0bcc06dae0cc7235ab731ddf210b072ea8ece9ff96b8abc5d138f36b
      ? ../Action/Type

let Action/toArguments =
        ../Action/toArguments sha256:5ff2f8297b445bbf401f9417f11be30c792e99345314f35344d325e52106fe2d
      ? ../Action/toArguments

let optionalOptions =
        ../../utils/optionalOptions sha256:51a1fbb37ceddab8f0ff0ef0fe2cd910af960bd447c6bcbc01b23426c0ee74f8
      ? ../../utils/optionalOptions

let noArguments = [] : List Text

let Options/toArguments =
      λ(opts : Options) →
          (if opts.silent then [ "-s" ] else noArguments)
        # (if opts.nostart then [ "--nostart" ] else noArguments)
        # (if opts.diffMode then [ "-d" ] else noArguments)
        # (if opts.previousWindow then [ "-l" ] else noArguments)
        # optionalOptions
            Text
            (λ(cmd : Text) → [ "-cc", cmd ])
            opts.beforeCommand
        # optionalOptions Text (λ(cmd : Text) → [ "-c", cmd ]) opts.afterCommand
        # optionalOptions
            Text
            (λ(name : Text) → [ "--servername", name ])
            opts.serverName
        # optionalOptions Action Action/toArguments opts.action

let test0 =
        assert
      :   Options/toArguments
            { Type = Options, default = Options/default }::{
            , silent = True
            , nostart = True
            , action = Some
                (Action.Split { vertical = True, files = [ "./foo", "./bar" ] })
            }
        ≡ [ "-s", "--nostart", "-O", "./foo", "./bar" ]

in  Options/toArguments
