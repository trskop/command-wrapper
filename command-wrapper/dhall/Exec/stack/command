-- vim: filetype=dhall

let ExecCommand =
      { Type =
            ../../CommandWrapper/ExecCommand/Type sha256:6ece797a2c269b469da41f12ec2d7206846cac65183ae8213cce1b6d59f2b02b
          ? ../../CommandWrapper/ExecCommand/Type
      , default =
            ../../CommandWrapper/ExecCommand/default sha256:c3a088ca2b090c91d5d630c4e01f4b4fbd0136f5bb1251f6828698e5180685b2
          ? ../../CommandWrapper/ExecCommand/default
      }

let ColourOutput =
        ../../CommandWrapper/ColourOutput/Type sha256:0ef91a3f044406ee80fc20f26127b448a0e98f46c46ec024457023d2aded8543
      ? ../../CommandWrapper/ColourOutput/Type

let Verbosity =
        ../../CommandWrapper/Verbosity/Type sha256:f009a39a49b1ee65651e9510778e7d72ff96820f4702a955e8f47682d72995c6
      ? ../../CommandWrapper/Verbosity/Type

let EnvironmentVariable =
        ../../CommandWrapper/EnvironmentVariable/Type sha256:b8c3c0c4ceb36ba4e6674df5de20ad1d97e120b93b9ce9914a41d0036770dcc4
      ? ../../CommandWrapper/EnvironmentVariable/Type

let ColourOutput/toText =
        ( ../../CommandWrapper/ColourOutput/package.dhall sha256:ab063b4e29e5237c1bbb2c4ed07ed3cc80096c9cd2f9978d727d81ac1f1c8c68
        ).toText
      ? (   ../../CommandWrapper/ColourOutput/package.dhall sha256:8fd5555f273cd4424f8fe2003154c4e36ea657bb5a2642d3677c4756b14c32e8
          ? ../../CommandWrapper/ColourOutput/package.dhall
        ).toText

let Verbosity/fold =
        ../../CommandWrapper/Verbosity/fold sha256:4dac2c264a2531d569ad0e5f712a1cd2d17b51ecdc502cc72f19937bf4733b1e
      ? ../../CommandWrapper/Verbosity/fold

let verbosityOption =
      Verbosity/fold
        (List Text)
        { Silent = [ "--silent" ]
        , Normal = [] : List Text
        , Verbose = [ "--verbosity=info" ]
        , Annoying = [ "--verbosity=debug" ]
        }

let ColourOutput/toArgument =
        ../utils/colorOption sha256:1974a5a3f18b441b9eae2f7cea20168ab26a97d450ad85a9e6160cea8538a170
      ? ../utils/colorOption

let Options =
        ./Options/Type sha256:b2526c288d5a0419d362c1375b879214396f6d1f6cd88b0416545724fd661b26
      ? ./Options/Type

let Options/toArguments =
        ./Options/toArguments sha256:2ec13c92b49f2238b0b0af9df65174c4bdc1bfba5baac87d568f08cb54bba1a5
      ? ./Options/toArguments

in    λ(options : Options)
    → λ(args : List Text)
    → λ(verbosity : Verbosity)
    → λ(colourOutput : ColourOutput)
    → λ(arguments : List Text)
    → ExecCommand::{
      , command = "stack"
      , arguments =
            verbosityOption verbosity
          # [ ColourOutput/toArgument colourOutput ]
          # Options/toArguments options
          # args
          # arguments
      , workingDirectory = options.workingDirectory
      }
