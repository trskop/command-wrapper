-- vim: filetype=dhall

let ShakeVerbosity =
        ./Type sha256:85aa28a8ed92e7924805f4501411c185014af50a9f3227b27fdaa17e495a68af
      ? ./Type

let lessThanEqual =
        https://prelude.dhall-lang.org/v17.0.0/Natural/lessThanEqual sha256:1a5caa2b80a42b9f58fff58e47ac0d9a9946d0b2d36c54034b8ddfe3cb0f3c99
      ? https://prelude.dhall-lang.org/v17.0.0/Natural/lessThanEqual

let subtract =
        https://prelude.dhall-lang.org/v17.0.0/Natural/subtract sha256:b9277ac637d09142a3a3ac79137ef5955c42f8b33b6746d59db2c9d75ccdd745
      ? https://prelude.dhall-lang.org/v17.0.0/Natural/subtract

let replicate =
        https://prelude.dhall-lang.org/v17.0.0/List/replicate sha256:d4250b45278f2d692302489ac3e78280acb238d27541c837ce46911ff3baa347
      ? https://prelude.dhall-lang.org/v17.0.0/List/replicate

let toNatural =
      λ(_ : ShakeVerbosity) →
        merge
          { Silent = 0
          , Error = 1
          , Warn = 2
          , Info = 3
          , Verbose = 4
          , Diagnostic = 5
          }
          _

let -- Function that figures out how to make the transition from current onto
    -- desired verbosity levels.
    --
    -- The logic behind `--quiet` and `--verbose` flags is:
    --
    -- * `--quiet` -- decrease one level
    -- * `--verbose` -- increase one level
    delta =
      λ(current : ShakeVerbosity) →
      λ(desired : ShakeVerbosity) →
        let currentNum = toNatural current
        
        let desiredNum = toNatural desired
        
        in  if    lessThanEqual currentNum desiredNum
            then  replicate (subtract currentNum desiredNum) Text "--verbose"
            else  replicate (subtract desiredNum currentNum) Text "--quiet"

let testDelta0 =
      assert : delta ShakeVerbosity.Info ShakeVerbosity.Info ≡ ([] : List Text)

let testDelta1 =
        assert
      :   delta ShakeVerbosity.Info ShakeVerbosity.Diagnostic
        ≡ [ "--verbose", "--verbose" ]

let testDelta1 =
        assert
      :   delta ShakeVerbosity.Info ShakeVerbosity.Silent
        ≡ [ "--quiet", "--quiet", "--quiet" ]

let -- Shake command line API doesn't make it easy to set a specific value in
    -- all cases and that's why we need to ask for the default value. Normally
    -- the default Shake verbosity is `Info`, see
    -- <https://hackage.haskell.org/package/shake/docs/src/Development.Shake.Internal.Options.html#shakeOptions>,
    -- however, that can be changed when constructing a custom Shake binary.
    toArguments =
      λ(default : ShakeVerbosity) →
      λ(desired : ShakeVerbosity) →
        merge
          { Silent = [ "--silent" ]
          , Error = delta default ShakeVerbosity.Error
          , Warn = delta default ShakeVerbosity.Warn
          , Info = delta default ShakeVerbosity.Info
          , Verbose = delta default ShakeVerbosity.Verbose
          , Diagnostic = [ "--debug" ]
          }
          desired

in    toArguments
    : ∀(default : ShakeVerbosity) → ∀(desired : ShakeVerbosity) → List Text
