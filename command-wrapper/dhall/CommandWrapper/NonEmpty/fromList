-- vim: filetype=dhall

let NonEmpty =
        ./Type sha256:e2e247455a858317e470e0e4affca8ac07f9f130570ece9cb7ac1f4ea3deb87f
      ? ./Type

let headAndTail =
        ../List/headAndTail sha256:06d3b92abb0790387092bf6db0ff9fc20299a898bb9e9a8e34373ad92b5ed86d
      ? ../List/headAndTail

let fromList =
      λ(a : Type) →
      λ(list : List a) →
        let x = headAndTail a list : { head : Optional a, tail : List a }
        
        in  merge
              { None = None (NonEmpty a)
              , Some = λ(just : a) → Some { head = just, tail = x.tail }
              }
              x.head

let example0 =
      assert : fromList Natural ([] : List Natural) ≡ None (NonEmpty Natural)

let example1 =
        assert
      :   fromList Natural [ 0 ]
        ≡ Some ({ head = 0, tail = [] : List Natural } : NonEmpty Natural)

let example2 =
        assert
      :   fromList Natural [ 0, 1, 2 ]
        ≡ Some ({ head = 0, tail = [ 1, 2 ] } : NonEmpty Natural)

in  fromList : ∀(a : Type) → List a → Optional (NonEmpty a)
